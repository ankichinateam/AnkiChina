plugins {
 // Gradle plugin portal
 id 'com.github.triplet.play' version '3.7.0'
}

apply plugin: 'com.android.application'
apply plugin: 'app.brant.amazonappstorepublisher'
apply plugin: 'idea'


repositories {
    google()
    mavenCentral()
    maven { url "https://jitpack.io" }
}


idea {
    module {
        downloadJavadoc = System.getenv("CI") != "true"
        downloadSources = System.getenv("CI") != "true"
    }
}

def homePath = System.properties['user.home']
android {
    compileSdkVersion 33 // change api compileSdkVersion at the same time

    defaultConfig {
        applicationId "com.app.ankichinas"
        manifestPlaceholders = [qqappid: ""]
        buildConfigField "Boolean", "CI", (System.getenv("CI") == "true").toString()
        buildConfigField "Boolean", "LEGACY_SCHEMA", "true"
        buildConfigField "String", "ACRA_URL", '"https://ankidroid.org/acra/report"'

        // The version number is of the form:
        // <major>.<minor>.<maintenance>[dev|alpha<build>|beta<build>|]
        // The <build> is only present for alpha and beta releases (e.g., 2.0.4alpha2 or 2.0.4beta4), developer builds do
        // not have a build number (e.g., 2.0.4dev) and official releases only have three components (e.g., 2.0.4).
        //
        // The version code is derived from the version name as follows:
        // AbbCCtDD
        // A: 1-digit decimal number representing the major version
        // bb: 2-digit decimal number representing the minor version
        // CC: 2-digit decimal number representing the maintenance version
        // t: 1-digit decimal number representing the type of the build
        // 0: developer build
        // 1: alpha release
        // 2: beta release
        // 3: public release
        // DD: 2-digit decimal number representing the build
        // 00 for internal builds and public releases
        // alpha/beta build number for alpha/beta releases
        //
        // This ensures the correct ordering between the various types of releases (dev < alpha < beta < release) which is
        // needed for upgrades to be offered correctly.
        versionCode=30104001
        versionName="3.1.4"
        minSdkVersion 21
        //noinspection OldTargetApi - also performed in api/build.fradle
        targetSdkVersion 29 // change .travis.yml platform download at same time
        testApplicationId "com.app.ankichinas"
        vectorDrawables.useSupportLibrary = true
        testInstrumentationRunner 'com.ichi2.testutils.NewCollectionPathTestRunner'
    }
    signingConfigs {
        release {
            storeFile file('anki_china.jks')
            storePassword 'ankichina'
            keyAlias = 'anki_china'
            keyPassword 'ankichina'
        }
    }
    productFlavors {
        channel1 {}
        channel2 {}
        channel3 {}
    }

    productFlavors.all {
        flavor -> flavor.manifestPlaceholders = [UMENG_CHANNEL_VALUE: name]
    }
    buildTypes {
        debug {
            debuggable true
            splits.abi.universalApk = true // Build universal APK for debug always
            // Check Crash Reports page on developer wiki for info on ACRA testing
            // buildConfigField "String", "ACRA_URL", '"https://918f7f55-f238-436c-b34f-c8b5f1331fe5-bluemix.cloudant.com/acra-ankidroid/_design/acra-storage/_update/report"'
            if (project.rootProject.file('local.properties').exists()) {
                Properties localProperties = new Properties()
                localProperties.load(project.rootProject.file('local.properties').newDataInputStream())
                // #6009 Allow optional disabling of JaCoCo for general build (assembleDebug).
                // jacocoDebug task was slow, hung, and wasn't required unless I wanted coverage
                testCoverageEnabled localProperties['enable_coverage'] != "false"
                // not profiled: optimization for build times
                if (localProperties['enable_languages'] == "false") {
                    android.defaultConfig.resConfigs "en"
                }
                // allow overriding default schema version
                if (localProperties["legacy_schema"] != null) {
                    buildConfigField "Boolean", "LEGACY_SCHEMA", localProperties["legacy_schema"]
                }
            } else {
                testCoverageEnabled true
            }
            signingConfig signingConfigs.release
            // make the icon red if in debug mode
            resValue 'color', 'anki_foreground_icon_color_0', "#FFFF0000"
            resValue 'color', 'anki_foreground_icon_color_1', "#FFFF0000"
        }
        release {
            minifyEnabled true
            splits.abi.universalApk = universalApkEnabled // Build universal APK for release with `-Duniversal-apk=true`
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
            resValue 'color', 'anki_foreground_icon_color_0', "#FF29B6F6"
            resValue 'color', 'anki_foreground_icon_color_1', "#FF0288D1"
        }
    }

    /**
     * Product Flavors are used for Amazon App Store and Google Play Store.
     * This is because we cannot use Camera Permissions in Amazon App Store (for FireTv etc...)
     * Therefore, different AndroidManifest for Camera Permissions is used in Amazon flavor.
     */
    flavorDimensions "appStore"
    productFlavors {
        play {
            dimension "appStore"
        }
        amazon {
            dimension "appStore"
        }
    }

    /**
     * Set this to true to create five separate APKs instead of one:
     *   - 2 APKs that only work on ARM/ARM64 devices
     *   - 2 APKs that only works on x86/x86_64 devices
     *   - a universal APK that works on all devices
     * The advantage is the size of most APKs is reduced by about 2.5MB.
     * Upload all the APKs to the Play Store and people will download
     * the correct one based on the CPU architecture of their device.
     */
    def enableSeparateBuildPerCPUArchitecture = true

    splits {
        abi {
            reset()
            enable enableSeparateBuildPerCPUArchitecture
            //universalApk enableUniversalApk  // set in debug + release config blocks above
            include "armeabi-v7a", "x86", "arm64-v8a", "x86_64"
        }
    }
    // applicationVariants are e.g. debug, release
    applicationVariants.all { variant ->

        // We want the same version stream for all ABIs in debug but for release we can split them
        if (variant.buildType.name == 'release') {
            variant.outputs.all { output ->

                // For each separate APK per architecture, set a unique version code as described here:
                // https://developer.android.com/studio/build/configure-apk-splits.html
                def versionCodes = ["armeabi-v7a": 1, "x86": 2, "arm64-v8a": 3, "x86_64": 4]
                def outputFile = output.outputFile
                if (outputFile != null && outputFile.name.endsWith('.apk')) {
                    def abi = output.getFilter("ABI")
                    if (abi != null) {  // null for the universal-debug, universal-release variants
                        //  From: https://developer.android.com/studio/publish/versioning#appversioning
                        //  "Warning: The greatest value Google Play allows for versionCode is 2100000000"
                        //  AnkiDroid versionCodes have a budget 8 digits (through AnkiDroid 9)
                        //  This style does ABI version code ranges with the 9th digit as 0-4.
                        //  This consumes ~20% of the version range space, w/50 years of versioning at our major-version pace
                        output.versionCodeOverride =
                                // ex:  321200106 = 3 * 100000000 + 21200106
                                versionCodes.get(abi) * 100000000 + defaultConfig.versionCode
                    }
                }
            }
        }
    }

    testOptions {
        animationsDisabled true
    }


    packagingOptions {
        exclude 'META-INF/DEPENDENCIES'
    }
    ndkVersion "22.0.7026061"
}

play {
    serviceAccountCredentials.set(file("${homePath}/src/AnkiDroid-GCP-Publish-Credentials.json"))
    track.set('alpha')
}
amazon {
    securityProfile = file("${homePath}/src/AnkiDroid-Amazon-Publish-Security-Profile.json")
    applicationId = "amzn1.devportal.mobileapp.524a424d314931494c55383833305539"
    pathToApks = [ file("./build/outputs/apk/amazon/release/AnkiDroid-amazon-universal-release.apk") ]
    replaceEdit = true
}

// Install Git pre-commit hook for Ktlint
task installGitHook(type: Copy) {
    from new File(rootProject.rootDir, 'pre-commit')
    into { new File(rootProject.rootDir, '.git/hooks') }
    fileMode 0755
}
tasks.getByPath(':AnkiDroid:preBuild').dependsOn installGitHook

// Issue 11078 - some emulators run, but run zero tests, and still report success
task assertNonzeroAndroidTests() {
    doLast {
        // androidTest currently creates one .xml file per emulator with aggregate results in this dir
        File folder = file("./build/outputs/androidTest-results/connected/flavors/play")
        File[] listOfFiles = folder.listFiles({ d, f -> f ==~ /.*.xml/ } as FilenameFilter)
        for (File file : listOfFiles) {
            // The aggregate results file currently contains a line with this pattern holding test count
            String[] matches = file.readLines().findAll { it.contains('<testsuite') }
            if (matches.length != 1) {
                throw new GradleScriptException("Unable to determine count of tests executed for " + file.name + ". Regex pattern out of date?", null)
            }
            if (!(matches[0] ==~ /.* tests="\d+" .*/) || matches[0].contains('tests="0"')) {
                throw new GradleScriptException("androidTest executed 0 tests for " + file.name + " - Probably a bug with the emulator. Try another image.", null)
            }
        }
    }
}
afterEvaluate {
    tasks.getByPath(':AnkiDroid:connectedPlayDebugAndroidTest').finalizedBy(assertNonzeroAndroidTests)
}

apply from: "./robolectricDownloader.gradle"
apply from: "./jacoco.gradle"
apply from: "../lint.gradle"

dependencies {
    configurations.all {
        resolutionStrategy {
            force 'org.jetbrains:annotations:23.0.0'
        }
    }

    lintChecks project(":lint-rules")
    implementation 'androidx.constraintlayout:constraintlayout:2.1.1'
    implementation project(path: ':RangeSeekBar')
//    implementation project(path: ':okhttp')
//    implementation project(path: ':okhttp')

    // Can possibly remove if upstream PR merges https://github.com/JakeWharton/timber/pull/398
    configurations.all {
        resolutionStrategy {
            force 'org.jetbrains:annotations:20.0.0'
        }
    }

    compileOnly 'org.jetbrains:annotations:20.1.0'
    compileOnly "com.google.auto.service:auto-service-annotations:1.0"
    annotationProcessor "com.google.auto.service:auto-service:1.0"

    implementation fileTree(dir: 'libs', include: ['*.jar'])
    // Note: the design support library can be quite buggy, so test everything thoroughly before updating it
    implementation 'com.google.android.material:material:1.4.0'
    implementation 'androidx.annotation:annotation:1.1.0'
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'androidx.browser:browser:1.2.0'
    implementation 'androidx.exifinterface:exifinterface:1.3.1'
    implementation 'androidx.localbroadcastmanager:localbroadcastmanager:1.0.0'
    implementation "androidx.multidex:multidex:2.0.1"
    implementation 'androidx.recyclerview:recyclerview:1.2.1'
    implementation 'androidx.sqlite:sqlite-framework:2.1.0'
    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.1.0'
    implementation 'androidx.viewpager2:viewpager2:1.0.0'
    implementation 'io.requery:sqlite-android:3.32.2'
    implementation 'com.afollestad.material-dialogs:core:0.9.6.0'
    implementation 'com.getbase:floatingactionbutton:1.10.1'
    // io.github.java-diff-utils:java-diff-utils is the natural successor here, but requires API24, #7091
    implementation 'org.bitbucket.cowwoc:diff-match-patch:1.2'
    // #6419  - API 27 (& maybe others) could not perform new ZipFile() on a 2GB+ apkg
    // noinspection GradleDependency - pinned at 1.12 until API21 minSdkVersion (File.toPath usage)
    implementation 'org.apache.commons:commons-compress:1.12'
    implementation 'com.github.bumptech.glide:glide:4.12.0'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.12.0'
    // May need a resolution strategy for support libs to our versions
    implementation'ch.acra:acra-http:5.7.0'
    implementation'ch.acra:acra-dialog:5.7.0'
    implementation'ch.acra:acra-toast:5.7.0'
    implementation'ch.acra:acra-limiter:5.7.0'

    implementation 'net.mikehardy:google-analytics-java7:2.0.13'
    //noinspection GradleDependency NewerVersionAvailable
    implementation 'com.squareup.okhttp3:okhttp:3.12.12'
    implementation 'com.arcao:slf4j-timber:3.1'

    implementation 'com.jakewharton.timber:timber:4.7.1'
    implementation 'com.google.code.gson:gson:2.8.6'

//    // == Rust conversion (from Anki-Android-Backend on GitHub) ==
//    String backendVersion = "0.1.8" // We want both testing and implementation on the same version
//    // build with ./gradlew rsdroid:assembleRelease
//    // In my experience, using `files()` currently requires a reindex operation, which is slow.
//    // implementation files("C:\\GitHub\\Rust-Test\\rsdroid\\build\\outputs\\aar\\rsdroid-release.aar")
//    implementation 'com.google.protobuf:protobuf-java:3.17.0' // This is required when loading from a file
//    implementation "io.github.david-allison-1:anki-android-backend:$backendVersion"
    // build with ./gradlew rsdroid-testing:assembleRelease
    // RobolectricTest.java: replace RustBackendLoader.init(); with RustBackendLoader.loadRsdroid(path);
    // A path for a testing library is typically under rsdroid-testing/assets
//    testImplementation "io.github.david-allison-1:anki-android-backend-testing:$backendVersion"

    implementation 'org.jsoup:jsoup:1.13.1'
//    implementation 'com.tencent.bugly:nativecrashreport:3.9.2'
//    implementation 'com.tencent.bugly:crashreport:4.1.9'
    api project(":api")

    testImplementation 'org.junit.vintage:junit-vintage-engine:5.6.2'
    testImplementation 'org.mockito:mockito-inline:3.5.7'
    testImplementation 'org.hamcrest:hamcrest-all:1.3'
    testImplementation 'net.lachlanmckee:timber-junit-rule:1.0.1'
    testImplementation "org.robolectric:robolectric:4.3.1"
    testImplementation 'androidx.test:core:1.3.0'
    testImplementation 'androidx.test.ext:junit:1.1.2'
    // debugImplementation required vs testImplementation: https://issuetracker.google.com/issues/128612536
    debugImplementation("androidx.fragment:fragment-testing:1.2.5") {
        // monitor dep constrained to 1.2 by fragment-testing, 1.3+ is needed: https://github.com/android/android-test/issues/481
        exclude group:'androidx.test', module:'monitor'
    }

    // May need a resolution strategy for support libs to our versions
    androidTestImplementation 'com.azimolabs.conditionwatcher:conditionwatcher:0.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'
    androidTestImplementation 'androidx.test.espresso:espresso-contrib:3.3.0'
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test:rules:1.3.0'
    androidTestImplementation 'org.smali:dexlib2:2.4.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'//正式版本

    implementation 'com.oguzdev:CircularFloatingActionMenu:1.0.2'
    //For AnkiDroid JS API Versioning
    implementation "com.github.zafarkhaja:java-semver:0.9.0"
    implementation 'com.umeng.umsdk:link:1.1.0'
    implementation  'com.umeng.umsdk:common:9.5.2'// 必选
    implementation  'com.umeng.umsdk:asms:1.6.3'// 必选
    implementation 'com.umeng.umsdk:apm:1.7.0' // 错误分析升级为独立产品U-APM可按需集成，可选

    implementation 'com.umeng.umsdk:share-core:7.2.0'
    implementation 'com.umeng.umsdk:share-board:7.2.0'
    implementation 'com.umeng.umsdk:share-qq:7.1.9'
    implementation 'com.umeng.umsdk:share-wx:7.1.9'
    implementation 'com.umeng.umsdk:share-sina:7.1.9'

    //新浪微博官方SDK
//    implementation 'com.sina.weibo.sdk:core:10.10.0:openDefaultRelease@aar'
    //QQ官方依赖库
    implementation 'com.tencent.tauth:qqopensdk:3.53.0'
    implementation 'com.google.zxing:core:3.4.1'
    //微信官方依赖库
    implementation 'com.tencent.mm.opensdk:wechat-sdk-android-without-mta:6.8.0'

}

